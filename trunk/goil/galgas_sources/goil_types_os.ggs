#
# @file goil_types_os.ggs
#
# @section File description
#
# Semantic types for os object.
#
# @section Copyright
#
# Goil OIL compiler for Trampoline
#
# Trampoline is copyright (c) IRCCyN 2005+
# Trampoline est protégé par la loi sur la propriété intellectuelle
#
# This software is distributed under the GNU Public Licence v2
#
# @section File informations
#
# $Date$
# $Rev$
# $Author$
# $URL$
#

semantics goil_types_os :

import semantics goil_basic_types in "goil_basic_types.ggs" ;

abstract class @trace_method {
  @location loc ;
  abstract method generate_c !@string res ;
}

class @trace_void extends @trace_method {
  override method generate_c !@string res :
    res := "#error \"TRACE_METHOD is not defined\"\n" ;
  end method ;
}

class @trace_file extends @trace_method {
  @lstring name ;
  override method generate_c !@string res :
    res := "#define    TRACE_FILE     \"".[name string]."\"\n" ;
  end method ;
}

class @trace {
  @location loc ;
  method generate_c !@string res :
    res := "#define    WITH_TRACE     NO\n" ;
  end method ;
  method generate_make !@string res :
    res := "WITH_TRACE=false\n" ;
  end method ;
}

class @trace_off extends @trace {
  override method generate_c !@string res :
    res := "#define    WITH_TRACE     NO\n" ;
  end method ;
  override method generate_make !@string res :
    res := "WITH_TRACE=false\n" ;
  end method ;
}

class @trace_on extends @trace {
  @lstring      form ;
  @trace_method meth ;
  override method generate_c !@string res :
    res := "#define    WITH_TRACE     YES\n" ;
    res .= "#define    TRACE_FORMAT   ".[form string]."\n" ;
    @string m ;
    [meth generate_c ?m] ;
    res .= m ;
  end method ;
  override method generate_make !@string res :
    res := "WITH_TRACE=true\n" ;
  end method ;
}

class @os_obj extends @oil_obj {
  @lstring     status ;
  @basic_type  startuphook ;
  @basic_type  shutdownhook ;
  @basic_type  errorhook ;
  @basic_type  pretaskhook ;
  @basic_type  posttaskhook ;
  @basic_type  protectionhook ;   # AUTOSAR
  @lstring     scalabilityclass ; # AUTOSAR
  @basic_type  stackmonitoring ;  # AUTOSAR
  @basic_type  usegetserviceid ;
  @basic_type  useparameteraccess ;
  @basic_type  useresscheduler ;
  @basic_type  systemcall ;
  @lstringlist app_src ;
  @lstringlist cflags ;
  @lstringlist asflags ;
  @lstringlist ldflags ;
  @lstring     app_name;
  @lstring     tpl_base_path;
  @trace       trace ;
  @ident_map   others ;
}

#
# set_method sets it first argument as the second one
# if it is unset (ie @void). Otherwise
# it outputs an error.
#
# @param  t     The method to set
# @param  s     The method used as source
# @param  att   The name of the attribute.
#               Used in the error message
#
routine set_method
  ?!@trace_method t
  ?@trace_method  s
  ?@string        att
:
  cast t :
  when == @trace_void do
    t := s ;
  else
    error [s loc] : "Redefinition of ".att ;
    error [t loc] : "was defined here" ;
  end cast ;
end routine ;

end semantics ;